/*
 * Copyright (C) 2014 Benny Bobaganoosh
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

struct BaseLight
{
    vec4 color;
    float intensity;
};

struct Attenuation
{
    float constant;
    float linear;
    float exponent;
};

struct DirectionalLight
{
    BaseLight base;
    vec3 direction;
};

struct PointLight
{
    BaseLight base;
    Attenuation attenuation;
    vec3 position;
    float range;
};

struct SpotLight
{
    PointLight pointLight;
    vec3 direction;
    float cutoff;
};

vec4 CalcLight(BaseLight base, vec3 direction, vec3 normal, vec3 worldPos, 
               float specularIntensity, float specularPower, vec3 eyePos)
{
	/**
	 * The cosine value of the angle between light direction and the surface normal vector.
	 * This value may also be used in the shadow calculation for optimized shadows (see http://www.sunandblackcat.com/tipFullView.php?l=eng&topicid=35)
	 * TODO: If the cosine is required in both light and shadow calculation then maybe we should determine the cosine once and pass it to CalcLight
	 * and CalcShadowAmount functions.
	 */
    float diffuseFactor = clamp(dot(normal, -direction), 0.0, 1.0);
    
    vec4 diffuseColor = base.color * base.intensity * diffuseFactor;
	/* ==================== Cartoon shading begin ==================== */
	//const int levels = 3;
	//const float scaleFactor = 1.0 / levels;
	//vec4 diffuseColor = (base.color * base.intensity * diffuseFactor) * floor(cosine * levels) * scaleFactor;
	/* ==================== Cartoon shading end ==================== */
        
    vec3 directionToEye = normalize(eyePos - worldPos);
    vec3 reflectDirection = normalize(reflect(direction, normal));
    
    float specularFactor = clamp(pow(clamp(dot(directionToEye, reflectDirection), 0.0, 1.0), specularPower), 0.0, 1.0);
	vec4 specularColor = base.color * specularIntensity * specularFactor;
    
    return diffuseColor + specularColor;
}

vec4 CalcPointLight(PointLight pointLight, vec3 normal, vec3 worldPos,
                    float specularIntensity, float specularPower, vec3 eyePos)
{
    vec3 lightDirection = worldPos - pointLight.position;
    float distanceToPoint = length(lightDirection);
    
    if(distanceToPoint > pointLight.range)
        return vec4(0,0,0,0);
    
    lightDirection = normalize(lightDirection);
    
    vec4 color = CalcLight(pointLight.base, lightDirection, normal, worldPos,
                           specularIntensity, specularPower, eyePos);
    
    float attenuation = pointLight.attenuation.constant +
                         pointLight.attenuation.linear * distanceToPoint +
                         pointLight.attenuation.exponent * distanceToPoint * distanceToPoint +
                         0.0001;
                         
    return color / attenuation;
}
